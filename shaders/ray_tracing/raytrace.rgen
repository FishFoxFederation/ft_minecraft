#version 460
#extension GL_EXT_ray_tracing : require

layout(set = 0, binding = 0, rgba32f) uniform image2D lighting_image;
layout(set = 0, binding = 1, rgba32f) uniform image2D shadow_image;
layout(set = 1, binding = 0) uniform accelerationStructureEXT topLevelAS;

layout(set = 2, binding = 0) uniform Camera
{
	mat4 view;
	mat4 proj;
	mat4 last_view;
	mat4 last_proj;
} camera;

layout(location = 0) rayPayloadEXT struct RayPayload
{
	vec3 ray_direction;
	vec4 color;
	float shadow;
	float depth;
	vec2 motion;
} payload;

bool bound_check(ivec2 pos, ivec2 size)
{
	return pos.x >= 0 && pos.x < size.x && pos.y >= 0 && pos.y < size.y;
}

void main()
{
	const vec2 pixel_center = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 inUV = pixel_center / vec2(imageSize(lighting_image));
	const vec2 d = inUV * 2.0 - 1.0;

	const mat4 inv_view = inverse(camera.view);
	const mat4 inv_proj = inverse(camera.proj);

	vec4 origin = inv_view * vec4(0, 0, 0, 1);
	vec4 target = inv_proj * vec4(d.x, d.y, 1, 1);
	vec4 direction = inv_view * vec4(normalize(target.xyz), 0);

	uint  rayFlags = gl_RayFlagsOpaqueEXT;
	float tMin = 0.001;
	float tMax = 1000.0;

	payload.ray_direction = direction.xyz;
	payload.color = vec4(0, 0, 0, 0);
	payload.shadow = 1.0;
	payload.depth = 1.0;
	payload.motion = vec2(0.0);

	traceRayEXT(
		topLevelAS,		// acceleration structure
		rayFlags,		// rayFlags
		0xFF,			// cullMask
		0,				// sbtRecordOffset
		0,				// sbtRecordStride
		0,				// missIndex
		origin.xyz,		// ray origin
		tMin,			// ray min range
		direction.xyz,	// ray direction
		tMax,			// ray max range
		0				// payload (location = 0)
	);

	ivec2 frame_pos = ivec2(gl_LaunchIDEXT.xy);
	ivec2 last_frame_pos = ivec2(vec2(frame_pos) - payload.motion);
	float shadow_last_frame = 0.0;
	if (bound_check(last_frame_pos, imageSize(shadow_image)))
	{
		shadow_last_frame = imageLoad(shadow_image, last_frame_pos).r;
		// shadow_last_frame = 0.9;
	}
	else
	{
		// shadow_last_frame = imageLoad(shadow_image, frame_pos).r;
		shadow_last_frame = 0.0;
	}
	float shadow = mix(shadow_last_frame, payload.shadow, 0.1);

	imageStore(lighting_image, frame_pos, payload.color);
	imageStore(shadow_image, frame_pos, vec4(shadow, payload.depth, 0.0, 0.0));
}
